plugins {
    id "java-library"
    id "checkstyle"
    id "jacoco"
}

dependencies {
    api project(":helper:helper")
    api project(":parser")
    api project(":avro-codegen")

    api "net.sf.jopt-simple:jopt-simple:5.0.4"
    implementation "org.apache.avro:avro-compiler:1.9.2"
    implementation "org.apache.avro:avro:1.9.2"
    implementation 'org.json:json:20220320'
    implementation "org.codehaus.jackson:jackson-core-asl:1.9.13"
    implementation "org.apache.logging.log4j:log4j-api:2.17.1"
    implementation "commons-io:commons-io:2.11.0"
    implementation "jakarta.json:jakarta.json-api:2.0.1"

    testImplementation "org.apache.avro:avro:1.9.2"
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            from project.components.java
            artifact sourceJar
            artifact javadocJar

            //we strive to meet https://central.sonatype.org/pages/requirements.html
            pom {
                name = 'Avro Util'
                description = 'utilities for writing code that works across major avro versions'
                url = 'https://github.com/linkedin/avro-util'

                licenses {
                    license {
                        name = 'BSD 2-Clause'
                        url = 'https://raw.githubusercontent.com/linkedin/avro-util/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'radai-rosenblatt'
                        name = 'Radai Rosenblatt'
                        email = 'radai.rosenblatt@gmail.com'
                        organization = 'LinkedIn'
                        organizationUrl = 'linkedin.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:linkedin/avro-util.git'
                    developerConnection = 'scm:git:ssh://github.com:linkedin/avro-util.git'
                    url = 'https://github.com/linkedin/avro-util'
                }
            }

            //remove the dependency on helper, and replace with one on helper-all
            pom.withXml {
                Node dependenciesNode = (Node) (asNode().dependencies[0])
                Collection<Node> dependencyNodes = dependenciesNode.children()
                List<Node> toRemove = new ArrayList<>()

                for (Node o : dependencyNodes) {
                    if ("$project.group" == o.groupId[0].text() && "helper" == o.artifactId[0].text()) {
                        toRemove.add(o)
                    }
                }

                for (Node o : toRemove) {
                    dependencyNodes.remove(o)
                }

                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', "$project.group")
                dependencyNode.appendNode('artifactId', "helper-all")
                dependencyNode.appendNode('version', "$project.version")
                dependencyNode.appendNode('scope', "compile")
            }
        }
    }
}
