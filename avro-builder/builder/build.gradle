plugins {
    id "java-library"
    id "checkstyle"
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

jar {
    manifest {
        attributes 'Main-Class': 'com.linkedin.avroutil1.builder.SchemaBuilder',
            'Implementation-Title': ' Avro builder',
            'Implementation-Version': project.version
    }
}

shadowJar {
    archiveClassifier = "all"

    //manifests is inherited from "normal" jar above

    dependencies {
        //do not pack any avro (user should bring their own)
        exclude(dependency("org.apache.avro:.*"))
    }
}

//produce fat jar as part of normal build
tasks.build.dependsOn tasks.shadowJar

dependencies {
    api project(":avro-builder:builder-spi")

    implementation "org.apache.logging.log4j:log4j-api:2.17.2"
    implementation "commons-io:commons-io:2.11.0"
    implementation "org.slf4j:slf4j-api:1.7.14"
    implementation "org.apache.avro:avro-compiler:1.9.2"
    implementation "org.apache.avro:avro:1.9.2"
    implementation 'org.json:json:20220320'
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            groupId project.group
            artifactId "$project.name"
            version project.version

            from components.java
            //the "-all" fat-jar is already published, and listing it here will cause an error over the dup
            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'Avro Util Builder'
                description = 'Utility for generating specific record classes out of avro schema files'
                url = 'https://github.com/linkedin/avro-util'

        	licenses {
                    license {
                        name = 'BSD 2-Clause'
                        url = 'https://raw.githubusercontent.com/linkedin/avro-util/master/LICENSE'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:linkedin/avro-util.git'
                    developerConnection = 'scm:git:ssh://github.com:linkedin/avro-util.git'
                    url = 'https://github.com/linkedin/avro-util'
                }
            }
        }

    }
}
