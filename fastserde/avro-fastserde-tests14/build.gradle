/*
 * Copyright 2020 LinkedIn Corp.
 * Licensed under the BSD 2-Clause License (the "License").
 * See License in the project root for license information.
 */

plugins {
  id "java-library"
}

sourceSets {
  test {
    java {
      srcDir "$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/java"
      srcDir "$buildDir/generated/avro"
      exclude "com/linkedin/avro/fastserde/logical/types/**" // LogicalType available since version 1.8
    }
    resources {
      srcDir "$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/resources"
    }
  }
}

dependencies {
  testImplementation project(":helper:helper")

  testImplementation "org.slf4j:slf4j-api:1.7.14"
  testImplementation "org.apache.commons:commons-lang3:3.4"
  testImplementation "com.sun.codemodel:codemodel:2.6"

  testImplementation ("org.apache.avro:avro:1.4.1") {
    exclude group: "org.mortbay.jetty"
    exclude group: "org.apache.velocity"
    exclude group: "commons-lang"
    exclude group: "org.jboss.netty"
    exclude group: "com.thoughtworks.paranamer", module: "paranamer-ant"
    exclude group: "org.slf4j"
  }

  testImplementation (project(":fastserde:avro-fastserde")) {
    exclude group: "org.apache.avro"
  }
  testImplementation ("org.apache.avro:avro:1.4.1") {
    exclude group: "org.mortbay.jetty"
    exclude group: "org.apache.velocity"
    exclude group: "commons-lang"
    exclude group: "org.jboss.netty"
    exclude group: "com.thoughtworks.paranamer", module: "paranamer-ant"
    exclude group: "org.slf4j"
  }

  testImplementation 'org.testng:testng:6.14.3'
  testImplementation 'org.slf4j:slf4j-simple:1.7.14'
}

test {
  testLogging.showStandardStreams = false

  useTestNG() {
    excludeGroups "perfTest"
  }

  testLogging {
    events "failed"
  }
}
task downloadAvro {
  doLast {
    project.mkdir "$buildDir"
    ant.get(src: "https://repo1.maven.org/maven2/org/apache/avro/avro/1.4.0/avro-tools-1.4.0.jar", dest: "$buildDir/avro-tools-1.4.0.jar")
  }
}

task runVanillaAvroCodegen {
  description = 'generate specific classes using vanilla avro'

  dependsOn downloadAvro
  // define input and output files so we can have incremental build when nothing changes
  inputs.dir("$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/avro")
  outputs.dir("$buildDir/generated/avro")

  fileTree(dir: "$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/avro", include:'**/*.avsc').each { file ->
    doLast {
      javaexec {
        classpath = files("$buildDir/avro-tools-1.4.0.jar")
        main = 'org.apache.avro.tool.Main'
        args = ["compile", "schema", file.getAbsolutePath(), "$buildDir/generated/avro"]
      }
    }
  }
}

compileTestJava.dependsOn runVanillaAvroCodegen
