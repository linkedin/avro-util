/*
 * Copyright 2020 LinkedIn Corp.
 * Licensed under the BSD 2-Clause License (the "License").
 * See License in the project root for license information.
 */

plugins {
  id "java-library"
}

configurations {
  codegen
}

sourceSets {
  test {
    java {
      srcDir "$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/java"
      srcDir "$buildDir/generated/avro"
    }
    resources {
      srcDir "$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/resources"
    }
  }
}

dependencies {
  testImplementation project(":helper:helper")

  testImplementation "org.slf4j:slf4j-api:1.7.14"
  testImplementation 'org.awaitility:awaitility:4.2.0'
  testImplementation "org.apache.commons:commons-lang3:3.4"
  testImplementation "com.sun.codemodel:codemodel:2.6"

  testImplementation ("org.apache.avro:avro:1.11.4") {
    exclude group: "org.slf4j"
  }

  testImplementation (project(":fastserde:avro-fastserde")) {
    exclude group: "org.apache.avro"
  }

  testImplementation 'org.testng:testng:6.14.3'
  testImplementation 'org.slf4j:slf4j-simple:1.7.14'

  codegen project(":helper:helper")

  // trevni-avro and trevni-core no longer publish -test jars, but avro-tools still tries to pull in these test jars
  // gradle does not support custom resolution strategy for classifier. The blocks below are to do a workaround by
  // excluding trevni-avro and trevni-core first and then add them back without the test classifier
  codegen ("org.apache.avro:avro-tools:1.11.4") {
    exclude group: "org.apache.avro", module: "trevni-avro"
    exclude group: "org.apache.avro", module: "trevni-core"
  }
  codegen ("org.apache.avro:trevni-avro:1.11.4") {
    exclude group: "org.apache.avro", module: "trevni-core"
  }
  codegen "org.apache.avro:trevni-core:1.11.4"
  codegen "org.apache.avro:avro-compiler:1.11.4"
}

test {
  testLogging.showStandardStreams = false

  useTestNG() {
    excludeGroups "perfTest"
  }

  testLogging {
    events "failed"
  }
}

task runVanillaAvroCodegen {
  description = 'generate specific classes using vanilla avro'

  dependsOn configurations.codegen
  // define input and output files so we can have incremental build when nothing changes
  inputs.dir("$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/avro")
  outputs.dir("$buildDir/generated/avro")

  fileTree(dir: "$rootProject.projectDir/fastserde/avro-fastserde-tests-common/src/test/avro", include:'**/*.avsc').each { file ->
    doLast {
      javaexec {
        classpath = configurations.codegen
        main = 'org.apache.avro.tool.Main'
        args = ["compile", "schema", file.getAbsolutePath(), "$buildDir/generated/avro"]
      }
    }
  }
}

compileTestJava.dependsOn runVanillaAvroCodegen
